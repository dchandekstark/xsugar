xmlns:xml = "http://www.w3.org/XML/1998/namespace"

// <date>, <persName>, <note>, <p>, and <underline>

NL = [\r\n]+

ANYSP = [\r\n \t]* (MAX)

DATE = \-?[0-9]{3}[1-9](\-(0[1-9]|1[0-2])(\-(0[1-9]|[1-3][0-9]))?)?
TEXT = [^<>\|\=#\/\*_]+ (MAX)
NUM = [0-9]+
WORD = [^\r\n \t<>\|\=#\/\*_]+ (MAX)

BEGP = "<="
ENDP = "=>"
BEGDIV = "<D="
ENDDIV = "=D>"

file
  : [BEGDIV] [ANYSP a] [paragraphs p] [ANYSP b] [ENDDIV] = <div>[ANYSP a] [paragraphs p] [ANYSP b]</>

paragraphs
  :   [paragraph p] [ANYSP a] [paragraphs more] = [paragraph p] [ANYSP a] [paragraphs more]
  >:  [paragraph p] = [paragraph p]

paragraph 
  : [BEGP] [items z] [ENDP] = <p>[items z]</> 
  >: [BEGP] [ENDP] = <p></> 

items
  :  [item i] [items more] = [item i] [items more]
  >: [item p] = [item p]

item
  >: [date d] = [date d]
  >: [persname pn] = [persname pn]
  >: [note n] = [note n]
  >: [underline u] = [underline u]
  >: [italic i] = [italic i]
  >: [sic_corr sc] = [sic_corr sc]
  >: [pb p] = [pb p]
  >: [item_can_nest t] = [item_can_nest t]

item_can_nest
  : [TEXT t] = [TEXT t]
  >: [WORD w] = [WORD w]

date
  : "<d#" [date_items a] "#d>" = <date>[date_items a]</>
  >: "<d#" [date_items a] "|" [DATE b] "#d>" = <date when=[DATE b]>[date_items a]</>
  >: "<d#" [date_items a] "|" [DATE b] "/" [DATE c] "#d>" = <date notBefore=[DATE b] notAfter=[DATE c]>[date_items a]</>

date_items
  : [date_item d] [date_items more] = [date_item d] [date_items more]
  >: [date_item d] = [date_item d]

date_item
  >: [persname pn] = [persname pn]
  >: [note n] = [note n]
  >: [underline u] = [underline u]
  >: [italic i] = [italic i]
  >: [sic_corr sc] = [sic_corr sc]
  >: [pb p] = [pb p]
  >: [item_can_nest t] = [item_can_nest t]

persname
  : "<pn#" [TEXT a] "#pn>" = <persName>[TEXT a]</> 
  : "<pn#" [TEXT a] "|" [URI ref] "#pn>" = <persName ref=[URI ref]>[TEXT a]</> 

note
  : "\/*" [NUM n] ". " [items b] "*\/" = <note n=[NUM n]>[items b]</>
  >: "\/*" [items a] "*\/" = <note>[items a]</>

underline
  : "_|" [underline_items a] "_|" = <hi rend="underline">[underline_items a]</>

underline_items
  : [underline_item u] [underline_items more] = [underline_item u] [underline_items more]
  >: [underline_item u] = [underline_item u]

underline_item
  >: [date d] = [date d]
  >: [persname pn] = [persname pn]
  >: [italic i] = [italic i]
  >: [sic_corr sc] = [sic_corr sc]
  >: [pb p] = [pb p]
  >: [item_can_nest t] = [item_can_nest t]

italic
  : "_/" [italic_items a] "_/" = <hi rend="italic">[italic_items a]</>

italic_items
  : [italic_item i] [italic_items more] = [italic_item i] [italic_items more]
  >: [italic_item i] = [italic_item i]

italic_item
  >: [date d] = [date d]
  >: [persname pn] = [persname pn]
  >: [underline u] = [underline u]
  >: [sic_corr sc] = [sic_corr sc]
  >: [pb p] = [pb p]
  >: [item_can_nest t] = [item_can_nest t]

sic_corr
  : "<sc#" [sic_corr_items a] "#sc>" = <sic>[sic_corr_items a]</>
  >: "<sc#" [sic_corr_items a] "|" [sic_corr_items b] "#sc>" = <choice>[ANYSP]<sic>[sic_corr_items a]</>[ANYSP]<corr>[sic_corr_items b]</>[ANYSP]</>

sic_corr_items
  : [sic_corr_item sc] [sic_corr_items more] = [sic_corr_item sc] [sic_corr_items more]
  >: [sic_corr_item sc] = [sic_corr_item sc]

sic_corr_item
  >: [date d] = [date d]
  >: [persname pn] = [persname pn]
  >: [underline u] = [underline u]
  >: [italic i] = [italic i]
  >: [pb p] = [pb p]
  >: [item_can_nest t] = [item_can_nest t]

pb
  : "<p#" [WORD n] ">" = <pb n=[WORD n]></>
  >: "<p#" [WORD n] "|" [WORD id] ">" = <pb xml:id=[WORD id] n=[WORD n]></>

